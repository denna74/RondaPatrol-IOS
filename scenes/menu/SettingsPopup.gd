extends Control

signal saved
signal cancelled

@onready var title_label: Label = $Panel/Margin/VBox/Title
@onready var language_label: Label = $Panel/Margin/VBox/LanguageRow/LanguageLabel
@onready var language_btn: TextureButton = $Panel/Margin/VBox/LanguageRow/LanguageButton
@onready var bgm_label: Label = $Panel/Margin/VBox/BgmLabel
@onready var bgm_slider: HSlider = $Panel/Margin/VBox/BgmRow/BgmSlider
@onready var bgm_pct: Label = $Panel/Margin/VBox/BgmRow/BgmPct
@onready var sfx_label: Label = $Panel/Margin/VBox/SfxLabel
@onready var sfx_slider: HSlider = $Panel/Margin/VBox/SfxRow/SfxSlider
@onready var sfx_pct: Label = $Panel/Margin/VBox/SfxRow/SfxPct
@onready var cancel_btn: TextureButton = $Panel/Margin/VBox/Buttons/CancelButton
@onready var save_btn: TextureButton = $Panel/Margin/VBox/Buttons/SaveButton

const ButtonBuilder := preload("res://scripts/ButtonBuilder.gd")
const EMPTY_SHORT := preload("res://assets/buttons/empty_short.png")
const FONT_BLUE := "res://assets/fonts/blue/"
const FONT_GREEN := "res://assets/fonts/green/"
const FONT_PINK := "res://assets/fonts/pink/"

var _snapshot_lang := "en"
var _snapshot_bgm := 4
var _snapshot_sfx := 4

func _ready():
	_build_circle_grabber(bgm_slider)
	_build_circle_grabber(sfx_slider)
	_snapshot_lang = TranslationManager.current_language
	_snapshot_bgm = MusicManager.bgm_level
	_snapshot_sfx = ClickPlayer.sfx_level
	bgm_slider.value = _snapshot_bgm
	sfx_slider.value = _snapshot_sfx
	_refresh_pct()
	bgm_slider.value_changed.connect(_on_bgm_changed)
	sfx_slider.value_changed.connect(_on_sfx_changed)
	language_btn.pressed.connect(_on_language_pressed)
	language_btn.pressed.connect(ClickPlayer.play)
	cancel_btn.pressed.connect(_on_cancel_pressed)
	cancel_btn.pressed.connect(ClickPlayer.play)
	save_btn.pressed.connect(_on_save_pressed)
	save_btn.pressed.connect(ClickPlayer.play)
	TranslationManager.language_changed.connect(_update_texts)
	_update_texts()

func _update_texts(_lang: String = ""):
	var t := TranslationManager
	title_label.text = t.t("settings")
	language_label.text = t.t("language_label")
	bgm_label.text = t.t("background_music")
	sfx_label.text = t.t("sound_effect")
	_apply_button_texture(language_btn, "ID" if t.current_language == "id" else "EN", FONT_BLUE)
	_apply_button_texture(save_btn, t.t("save"), FONT_GREEN)
	_apply_button_texture(cancel_btn, t.t("cancel"), FONT_PINK)

func _apply_button_texture(btn: TextureButton, text: String, font_dir: String) -> void:
	btn.texture_normal = ButtonBuilder.build_button_texture(text, font_dir, Vector2i(22, 28), EMPTY_SHORT)
	btn.texture_pressed = ButtonBuilder.darken_texture(btn.texture_normal)

func _build_circle_grabber(slider: HSlider) -> void:
	var size := 36
	var img := Image.create(size, size, false, Image.FORMAT_RGBA8)
	img.fill(Color(0, 0, 0, 0))
	var center := Vector2(size / 2.0, size / 2.0)
	var radius := size / 2.0
	for y in size:
		for x in size:
			var d := Vector2(x + 0.5, y + 0.5).distance_to(center)
			if d <= radius - 0.5:
				img.set_pixel(x, y, Color(0.6, 0.6, 0.6, 1))
			elif d < radius + 0.5:
				var a := 1.0 - (d - (radius - 0.5))
				img.set_pixel(x, y, Color(0.6, 0.6, 0.6, clamp(a, 0.0, 1.0)))
	var tex := ImageTexture.create_from_image(img)
	slider.add_theme_icon_override("grabber", tex)
	slider.add_theme_icon_override("grabber_highlight", tex)
	slider.add_theme_icon_override("grabber_disabled", tex)

func _refresh_pct():
	bgm_pct.text = "%d%%" % int(bgm_slider.value * 25)
	sfx_pct.text = "%d%%" % int(sfx_slider.value * 25)

func _on_bgm_changed(value: float):
	MusicManager.set_bgm_level(int(value))
	_refresh_pct()

func _on_sfx_changed(value: float):
	ClickPlayer.set_sfx_level(int(value))
	ClickPlayer.play()
	_refresh_pct()

func _on_language_pressed():
	TranslationManager.set_language("id" if TranslationManager.current_language == "en" else "en")

func _on_cancel_pressed():
	TranslationManager.set_language(_snapshot_lang)
	MusicManager.set_bgm_level(_snapshot_bgm)
	ClickPlayer.set_sfx_level(_snapshot_sfx)
	cancelled.emit()
	queue_free()

func _on_save_pressed():
	saved.emit()
	queue_free()
